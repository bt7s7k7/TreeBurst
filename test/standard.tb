// Test: assert success
assert(true)

// Test: assert fail, expect fail
assert(false)

// Test: comparison operators
assert(true == true)
assert(false == false)
assert(true != false)
assert(1 != "")
assert(1 == 1)
assert(1 != 2)
assert(void != null)
assert(void == void)

// Test: assert equal
assertEqual(1, 1)
assertEqual("one", "one")

// Test: assert unequal, expect fail
assertEqual(1, 2)

// Test: parsing rollback
assertEqual((1 0), 0)

// Test: logical operators
assertEqual(false && false, false)
assertEqual(true && false, false)
assertEqual(false && true, false)
assertEqual(true && true, true)

assertEqual(false || false, false)
assertEqual(true || false, true)
assertEqual(false || true, true)

assertEqual(getCounter(), 0)

1 && increment()
assertEqual(getCounter(), 1)

0 && increment()
assertEqual(getCounter(), 1)

1 || increment()
assertEqual(getCounter(), 1)

0 || increment()
assertEqual(getCounter(), 2)

// Test: coalescing
assertEqual(false ?? 0, false)
assertEqual(null ?? 0, 0)
assertEqual(void ?? 0, 0)
assertEqual(null else 0, null)
assertEqual(void else 0, 0)

// Test: numeric operators
assertEqual(1 + 1.5, 2.5)
assertEqual(1 - 1.5, -0.5)
assertEqual(1 * 1.5, 1.5)
assertEqual(2 / 4, 0.5)
assertEqual(5 % 2, 1)
assertEqual(-(1 + 1), -2)
assertEqual(3 | 4, 7)
assertEqual(3 & 1, 1)
assertEqual(3 ^ 1, 2)

// Test: numeric comparison operators
assertEqual(1 > 2, false)
assertEqual(1 < 2, true)
assertEqual(2 <= 2, true)
assertEqual(3 <= 2, false)
assertEqual(3 >= 2, true)
assertEqual(3 >= 3, true)

// Test: numeric operators typecheck, expect fail
void + 1

// Test: if
assertEqual(@if(true 1 0), 1)
assertEqual(@if(false 1 0), 0)
assertEqual(@if(false 1), void)

// Test: function declaration
assertEqual((\4)(), 4)
assertEqual((\(a) a + 1)(2), 3)

// Test: variable declaration
$x = 1
assertEqual(x, 1)
assertEqual($y = 2, 2)
assertEqual(x = x + 1, 2)
assertEqual(x, 2)

// Test: variable declaration duplicate, expect fail
$x = 1
$x = 2

// Test: function call method
$owner = Table.new()
$owner.method = \(this, arg) {
    assertEqual(this, other)
    assertEqual(arg, 5)
    increment()
}

$other = Table.new()
owner.method.call(other, [5])
assertEqual(getCounter(), 1)

// Test: table new
$table = Table.new()
$table.name = "foo"
assertEqual(table.name, "foo")

// Test: nested tables
$a = Table.new()
$a.b = Table.new()
$a.b.c = 5

assertEqual(a.b.c, 5)

// Test: table property mutation
$table = Table.new()
$table.x = 1
assertEqual(table.x, 1)
table.x = 2
assertEqual(table.x, 2)

// Test: table duplicate property, expect fail
$table = Table.new()
$table.name = "foo"
$table.name = "foo2"

// Test: table method declaration
$table = Table.new()
$table.foo = \(this, arg) {
    assertEqual(this, table)
    assertEqual(arg, 24)
    increment()
}

table.foo(24)
assertEqual(getCounter(), 1)

$table.bar = \(a, b) {
    assertEqual(a, "a")
    assertEqual(b, "b")
    increment()
}

table.bar("a", "b")
assertEqual(getCounter(), 2)

// Test: table method declaration 2
$Foo = Table.new()
$Foo.prototype = Table.new()
$value = 0

$Foo.prototype.getValue = \value
$Foo.prototype.setValue = \(new) value = new

// Test: inheritance
$Foo = Table.new()
$Foo.prototype = Table.new()
$Foo.new = Table.new

$Foo.prototype.method = \(this) assertEqual(this, foo)

$foo = Foo.new()
foo.method()

// Test: table void prevention 1, expect fail
$x = Table.new()
$x.value = void

// Test: table void prevention 2, expect fail
$x = Table.new()
$x.value = 5
x.value = void

// Test: native type extension
$Number.prototype.plusOne = \(this) this + 1

assertEqual((1).plusOne(), 2)

// Test: checking setting properties, expect fail
"text".x = 5

// Test: checking declaring properties, expect fail
$"text".x = 5

// Test: array
$x = [1,2,3]
assertEqual(x.length, 3)
assertEqual(x[0], 1)
assertEqual(x[1], 2)
assertEqual(x[-1], 3)
assertEqual(x[-2], 2)

x[0] = 5
assertEqual(x[0], 5)

// Test: array try
$x = []
assertEqual(x.tryAt(10), void)
x.tryAt(10) = 5
assertEqual(x[10], 5)
assertEqual(x[9], null)

// Test: array bounds check 1, expect fail
$x = [1,2,3]
x[4]

// Test: array bounds check 2, expect fail
$x = [1,2,3]
x[-4]

// Test: array void check, expect fail
$x = [1]
x[0] = void

// Test: array truncate
$x = []
x.truncate(2)
assertEqual(x.length, 2)
assertEqual(x[0], null)
assertEqual(x[1], null)
x.truncate(0)
assertEqual(x.length, 0)

// Test: array clone
$x = [1, 2, 3]
$y = x.clone()

assertEqual(x.length, y.length)
assertEqual(x[0], y[0])
assertEqual(x[1], y[1])
assertEqual(x[2], y[2])

// Test: array clear
$x = [1, 2, 3]
x.clear()
assertEqual(x.length, 0)

// Test: array slice
$x = [0, 1, 2, 3]
$y

y = x.slice(2)
assertEqual(y.length, 2)
assertEqual(y[0], 2)
assertEqual(y[1], 3)

y = x.slice(1, 3)
assertEqual(y.length, 2)
assertEqual(y[0], 1)
assertEqual(y[1], 2)

y = x.slice(-2)
assertEqual(y.length, 2)
assertEqual(y[0], 2)
assertEqual(y[1], 3)

y = x.slice(0, -2)
assertEqual(y.length, 2)
assertEqual(y[0], 0)
assertEqual(y[1], 1)

y = x.slice(-2, -1)
assertEqual(y.length, 1)
assertEqual(y[0], 2)

// Test: array splice
$x = [1, 2, 3]
$y

y = x.clone()
y.splice(1, 1)
assertEqual(y.length, 2)
assertEqual(y[0], 1)
assertEqual(y[1], 3)

y = x.clone()
y.splice(1, 1, [5, 6])
assertEqual(y.length, 4)
assertEqual(y[0], 1)
assertEqual(y[1], 5)
assertEqual(y[2], 6)
assertEqual(y[3], 3)

y = x.clone()
y.splice(y.length, 0, [4])
assertEqual(y.length, 4)
assertEqual(y[3], 4)

// Test: array push
$x = [1]
x.push(2, 3)
assertEqual(x.length, 3)
assertEqual(x[1], 2)
assertEqual(x[2], 3)

// Test: array append
$x = [1]
x.append([2, 3])
assertEqual(x.length, 3)
assertEqual(x[1], 2)
assertEqual(x[2], 3)

// Test: array pop
$x = [1, 2]
$last = x.pop()
assertEqual(x.length, 1)
assertEqual(last, 2)

// Test: array unshift
$x = [1]
x.unshift(-1, "pre")
assertEqual(x.length, 3)
assertEqual(x[0], -1)
assertEqual(x[1], "pre")

// Test: array prepend
$x = [1]
x.prepend([-1, "pre"])
assertEqual(x.length, 3)
assertEqual(x[0], -1)
assertEqual(x[1], "pre")

// Test: array map
$x = [1, 2, 3]

assertEqual(x.map(\(v) v + 1).k_dump(1), "[2, 3, 4]")

assertEqual(x.map(\(v) @if(v == 2, void, v)).k_dump(1), "[1, 3]")

assertEqual(x.map(\(_, i) i).k_dump(1), "[0, 1, 2]")

$y = x.map(\(_, _, a) a)
assertEqual(y[0], x)
assertEqual(y[1], x)
assertEqual(y[2], x)

// Test: array filter
$x = [1, 2, 3]

assertEqual(x.filter(\true).k_dump(1), "[1, 2, 3]")
assertEqual(x.filter(\(v) v > 1).k_dump(1), "[2, 3]")

// Test: map
$a = 5
$map = {a, b: true, [1]: "c"}
assertEqual(map.length, 3)
assertEqual(map["a"], 5)
assertEqual(map["b"], true)
assertEqual(map[1], "c")
assertEqual(map["other"], void)

map["a"] = 6
assertEqual(map["a"], 6)

map["a"] = void
assertEqual(map["a"], void)
assertEqual(map.length, 2)

// Test: map clear
$map = {a: 1, b: 2, c: 3}
map.clear()
assertEqual(map.length, 0)

// Test: map clone
$map = {a: 1, b: 2, c: 3}
$other = map.clone()

assertEqual(map.length, other.length)
assertEqual(map["a"], other["a"])
assertEqual(map["b"], other["b"])
assertEqual(map["c"], other["c"])

// Test: map keys
$map = {a: 1, b: 2, c: 3}
$keys = map.keys()

assertEqual(keys.length, 3)
assertEqual(keys[0], "a")
assertEqual(keys[1], "b")
assertEqual(keys[2], "c")

// Test: map values
$map = {a: 1, b: 2, c: 3}
$values = map.values()

assertEqual(values.length, 3)
assertEqual(values[0], 1)
assertEqual(values[1], 2)
assertEqual(values[2], 3)

// Test: map entries
$map = {a: 1, b: 2, c: 3}
$entries = map.entries()

assertEqual(entries.length, 3)
assertEqual(entries[0][0], "a")
assertEqual(entries[0][1], 1)

assertEqual(entries[1][0], "b")
assertEqual(entries[1][1], 2)

assertEqual(entries[2][0], "c")
assertEqual(entries[2][1], 3)

// Test: unreachable, expect fail
unreachable()

// Test: while
$list = [1, 2, 3]
$i = 0

@while(i < list.length, (
    i = i + 1
    increment()
))

assertEqual(getCounter(), 3)

// Test: implicit string argument
$thunk = \(v) {
    assertEqual(v, "foo")
    increment()
}

thunk"foo"
assertEqual(getCounter(), 1)
assertEqual("bar")"bar"

$state = { q: 0 }
$s = \(key, value) {
    state[key] = value
}

s"q" = 5
assertEqual(state["q"], 5)

// Test: return statement
$thunk = \{
    increment()
    return(5)
    increment()
}

assertEqual(thunk(), 5)
assertEqual(getCounter(), 1)

// Test: return statement 2
$thunk = \{
    $i = 0
    @while(true, (
        @if(i > 10, (
            return(i)
        ))

        i = i + 1
    ))
}

// Test: return statement 3
return()
assertEqual(false, true)

// Test: goto statement

goto("skip")

assertEqual(true, false)

skip:

// Test: goto statement 2
    $i = 0
start:
    @if(i > 10, goto("end"))
    i = i + 1
    goto("start")
end:

// Test: advanced assignment
$i = 5
i += 10
assertEqual(i, 15)

$table = Table.new()
$table.q = 5
table.q += 10
assertEqual(table.q, 15)

$map = {q: 5}
map["q"] += 10
assertEqual(map["q"], 15)

// Test: advanced assignment short-circuit
$a = false
a ||= (increment(), true)
assertEqual(a, true)
assertEqual(getCounter(), 1)

a ||= (increment(), true)
assertEqual(a, true)
assertEqual(getCounter(), 1)

$b = void
b else= (increment(), 10)
assertEqual(b, 10)
assertEqual(getCounter(), 2)

// Test: getters
$foo = Table.new()
$value = 0
$foo.get_value = \value

assertEqual(foo.value, value)
value = 15
assertEqual(foo.value, value)

// Test: setters
$foo = Table.new()
$value = 0
$foo.set_value = \(newValue) value = newValue

foo.value = 15
assertEqual(value, 15)

// Test: get/setter inheritance
$Foo = Table.new()
$Foo.new = Table.new
$Foo.prototype = Table.new()

$value = 0

$Foo.prototype.get_value = \(this){
    assertEqual(this, foo)
    value
}

$Foo.prototype.set_value = \(this, new) {
    assertEqual(this, foo)
    value = new
}

$foo = Foo.new()

assertEqual(foo.value, value)
value = 15
assertEqual(foo.value, value)
foo.value = 20
assertEqual(value, 20)
$foo.value = 25
assertEqual(value, 20)
assertEqual(foo.value, 25)

// Test: native wrapper
assertEqual(dummy.a, 0)
assertEqual(dummy.b, 0)

dummy.a = 5
dummy.b = 10

assertEqual(dummy.a, 5)
assertEqual(dummy.b, 10)
assertEqual(dummy.sum, 15)

// Test: native wrapper map access

assertEqual(dummy.length, 0)

dummy["a"] = 1
dummy["b"] = 2
dummy["c"] = 3

assertEqual(dummy.length, 3)
assertEqual(dummy["a"], 1)
assertEqual(dummy["b"], 2)
assertEqual(dummy["c"], 3)

$entries = dummy.entries()

assertEqual(entries.length, 3)
assertEqual(entries[0][0], "a")
assertEqual(entries[0][1], 1)

assertEqual(entries[1][0], "b")
assertEqual(entries[1][1], 2)

assertEqual(entries[2][0], "c")
assertEqual(entries[2][1], 3)

dummy.clear()
assertEqual(dummy.length, 0)

// Test: string conversion
assertEqual((1).k_string(), "1")
assertEqual((true).k_string(), "true")
assertEqual(([1, true]).k_string(), "[1, true]")
assertEqual(({}).k_string(), "{}")
assertEqual((Table.new).k_string(), "function Table.new(this)")
assertEqual((Table.new()).k_string(), "Table()")

// Test: native object dump
dummy["a"] = 1
dummy["b"] = 2
dummy["c"] = 3

assertEqual(dummy.k_string(), "DummyObject {a: 1, b: 2, c: 3}")

// Test: string escape
assertEqual("\n".k_dump(), `"\\n"`)
assertEqual('"'.k_dump(), `"\\""`)

// Test: string concatenation
assertEqual("a," + "b", "a,b")

assertEqual("a," + 1, "a,1")
assertEqual(1 + ",a", "1,a")

assertEqual("a," + null, "a,null")
assertEqual(null + ",a", "null,a")

assertEqual("a" + void, "a")
assertEqual(void + "a", "a")

// Test: pipeline operator
1 -> $variable
assertEqual(variable, 1)
2 -> variable
assertEqual(variable, 2)
3 -> variable -> $other
assertEqual(variable, 3)
assertEqual(variable, other)

$table = Table.new()

1 -> $table.foo
assertEqual(table.foo, 1)

$step1 = \(v) v + "1"
$step2 = \(v) v + "2"

"s" 
-> step1()
-> step2()
-> $output
assertEqual(output, "s12")

$array = []
"element" -> array.tryAt(0)
assertEqual(array[0], "element")

// Test: conditional operator
assertEqual(true ? "a" : "b", "a")
assertEqual(false ? "a" : 5 + 7, 12)
assertEqual(false ? "a" : true ? 5 + 7 : 4 / 2, 12)
assertEqual(false ? "a" : false ? 5 + 7 : 4 / 2, 2)

// Test: placeholder
$table = Table.new()
$table.value = 5

assertEqual((\?.value)(table), 5)

$container = Table.new()
$container.table = table

assertEqual((\?.table.value)(container), 5)

$result = Table.new()
$result.value = 15

$func = \(v) {
    assertEqual(v, 1)
    result
}

assertEqual((\func(?))(1), result)

$table.method = \(a, b, c) {
    assertEqual(a, 1)
    assertEqual(b, 2)
    assertEqual(c, 3)

    return(result)
}

assertEqual((\?.table.method(1, ?, 3))(container, 2), result)
assertEqual((\?.table.method(1, ?, 3).value)(container, 2), 15)

$inc = \? + 1
assertEqual(inc(5), 6)

$x = [1, 2, 3]
assertEqual(x.map(\? + 1).k_string(), "[2, 3, 4]")

$y = [\1, \2, \3]
assertEqual(y.map(\?()).k_string(), "[1, 2, 3]")
